apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'

final stampVersionCode = hasProperty('version_code') ? property('version_code').toInteger() : 1
final stampVersionName = hasProperty('version_name') ? '0.1.0.' + property('version_name') : '0.1.0.0'

final stampKeystorePassword = hasProperty('keystore_password') ? property('keystore_password') : ''
final stampKeyAlias = hasProperty('key_alias') ? property('key_alias') : ''
final stampKeyPassword = hasProperty('key_password') ? property('key_password') : ''
final stampKeyFile = hasProperty('key_file') ? property('key_file') : 'ci/debug.jks'
final stampPlayAPIJson = hasProperty('play_api_json') ? property('play_api_json') : 'ci/play.json'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        applicationId "com.sjn.stamp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode stampVersionCode
        versionName stampVersionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError true
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
    signingConfigs {
        src {
            storeFile file(stampKeyFile)
            storePassword stampKeystorePassword
            keyAlias stampKeyAlias
            keyPassword stampKeyPassword
        }
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.src
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all {
        variant -> variant.resValue "string", "versionName", variant.versionName
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$versions.supportLib"
    implementation "com.android.support:cardview-v7:$versions.supportLib"
    implementation "com.android.support:leanback-v17:$versions.supportLib"
    implementation "com.android.support:design:$versions.supportLib"
    implementation "com.android.support:mediarouter-v7:$versions.supportLib"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.google.android.gms:play-services-cast-framework:11.8.0'
    implementation project(':exoplayer-library-core')
    implementation project(':exoplayer-extension-flac')
    implementation 'com.google.firebase:firebase-analytics:11.8.0'
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-crash:11.8.0'
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'
    implementation('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }
    implementation 'com.github.sjnyag:AnimationWrapLayout:0.1.0'
    implementation 'com.github.takusemba:spotlight:1.1.1'
    implementation 'com.github.vajro:MaterialDesignLibrary:1.6'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation('com.mikepenz:materialdrawer:5.3.0@aar') {
        transitive = true
    }
    implementation 'com.nanohttpd:nanohttpd-webserver:2.1.1'
    implementation 'com.sothree.slidinguppanel:library:3.3.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.takisoft.fix:preference-v7:$versions.supportLib.0"
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc3'
    implementation 'net.danlew:android.joda:2.6.0'
    implementation 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"
    kapt 'io.realm:realm-annotations:3.5.0'
    kapt 'io.realm:realm-annotations-processor:3.5.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation "com.android.support:support-annotations:$versions.supportLib"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

play {
    track = 'alpha'
    jsonFile = file(stampPlayAPIJson)
}
apply plugin: 'com.google.gms.google-services'